plugins {
    id 'org.springframework.boot' version '2.5.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'com.diffplug.gradle.spotless' version '4.5.1'
    id "com.github.spotbugs" version "4.7.1"
    id 'com.google.cloud.tools.jib' version '2.6.0'
    id 'java'
}

group = 'io.terrible'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // OpenCV
    implementation 'org.openpnp:opencv:4.5.1-2'

    // Apache
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'commons-io:commons-io:2.11.0'

    // Google
    implementation 'com.google.guava:guava:30.1.1-jre'

    // Development
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
}

test {
    useJUnitPlatform()
}

spotless {
    java {
        licenseHeader '/* Licensed under Apache-2.0 */'
        importOrder 'java', 'javax', 'org', 'com', ''
        removeUnusedImports()
        googleJavaFormat()
    }
}

jib {
    to {
        image = 'peavers/terrible-face-detector'
        auth {
            username = System.getenv("DOCKER_USER")
            password = System.getenv("DOCKER_PASS")
        }
        tags = ["latest", "${project.name}-${project.version}".toString()]
    }
    container {
        creationTime = "USE_CURRENT_TIMESTAMP"
        jvmFlags = ['-Dfile.encoding=UTF-8']
        ports = ['8080']
        labels = [maintainer: 'Chris Turner<peavers@gmail.com>']
    }
}

build.dependsOn spotlessJavaApply
